pkgname='dnsrecon'
pkgver=0.8.9
pkgrel=1
pkgdesc="Python script for enumeration of hosts, subdomains and emails from a given domain using google."
url="https://github.com/darkoperator/dnsrecon"
arch=('i686' 'x86_64')
license=('custom')
depends=('python2')
makedepends=('python2-pip')
options=('!strip' '!emptydirs')
source=("https://github.com/darkoperator/dnsrecon/archive/v${pkgver}.tar.gz")
sha256sums=('c3b99b423919ed877dd75893212c0365c1491f2abca3de411cee04c467d8500a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d "vendor/lib/python2.7/site-packages/"
  PYTHONUSERBASE="${srcdir}/${pkgname}-${pkgver}/vendor" pip2 install --user --ignore-installed --requirement requirements.txt
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -d "$pkgdir/usr/bin" "$pkgdir/usr/share/${pkgname}"
  cp -r . "$pkgdir/usr/share/${pkgname}"

  local _binaries='dnsrecon.py'
  for bin in $_binaries; do
    local _wrapper="$pkgdir/usr/bin/$bin"
    echo -e "#!/bin/sh\nPYTHONPATH="/usr/share/${pkgname}:/usr/share/${pkgname}/vendor/lib/python2.7/site-packages" exec python2 /usr/share/${pkgname}/$bin \"\${@}\"" > "${_wrapper}"
    chmod 755 "${_wrapper}"
  done
}
