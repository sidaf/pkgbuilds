_pkgname=sslscan
pkgname=${_pkgname}-rbsec
pkgver=1.11.10
pkgrel=1
pkgdesc="A tool to scan SSL and TLS based services, to determine what ciphers and protocols are supported"
url='https://github.com/rbsec/sslscan'
arch=('i686' 'x86_64')
license=('GPL3')
makedepends=('git' 'perl')
conflicts=('sslscan')
source=("https://github.com/rbsec/sslscan/archive/${pkgver}-rbsec.tar.gz")
sha1sums=('b0ef2b5831275b4a2e553e8c73e3f2be1a84ffa8')

prepare() {
  cd ${_pkgname}-${pkgver}-rbsec
  sed 's/LDFLAGS      \+=/LDFLAGS      =/' -i Makefile
  sed 's/CFLAGS       \+=/CFLAGS       =/' -i Makefile
}

build() {
  cd ${_pkgname}-${pkgver}-rbsec
  make static
}

package() {
  cd ${_pkgname}-${pkgver}-rbsec
  install -d "$pkgdir/usr/bin" "$pkgdir/usr/share/man/man1"
  install -Dm755 sslscan "$pkgdir/usr/bin"
  install -Dm644 sslscan.1 "$pkgdir/usr/share/man/man1"
}
