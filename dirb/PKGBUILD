pkgname='dirb'
pkgver='2.22'
pkgrel=2
pkgdesc='A web content scanner, brute forceing for hidden files.'
url='http://dirb.sourceforge.net/'
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
license=('GPL2')
depends=('curl')
source=("http://downloads.sourceforge.net/project/dirb/dirb/${pkgver}/dirb${pkgver//./}.tar.gz")
sha1sums=('21851ad341357917a9672c834ccf90509c957f75')

prepare() {
  chmod -R 744 dirb${pkgver//./}
}

build () {
  cd "dirb${pkgver//./}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "dirb${pkgver//./}"

  make DESTDIR="${pkgdir}" install

  install -d "$pkgdir/usr/share/${pkgname}"
  cp -a "wordlists" "$pkgdir/usr/share/${pkgname}/"
  chmod -R 644 "$pkgdir/usr/share/${pkgname}/wordlists"
  chmod -R +X "$pkgdir/usr/share/${pkgname}/wordlists"

  # Fix conflict with extra/icu.
  mv "${pkgdir}/usr/bin/gendict" "${pkgdir}/usr/bin/dirb-gendict"
}
