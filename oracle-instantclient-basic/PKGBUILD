# Contributor: Adam Nielsen <malvineous@shikadi.net>
# Contributor: Vitaliy Berdinskikh ur6lad[at]archlinux.org.ua
# Contributor: Andrea Agosti <cifvts@gmail.com>
# Contributor: Charles Clement <caratorn@gmail.com>
# Contributor: Geraldo Augusto Vecchiato <geraldoav@gmail.com>
# Maintainer: Adam Nielsen <malvineous@shikadi.net>

DLAGENTS+=("file::/usr/bin/curl -o %o %u");

_pkgname=instantclient-basic
pkgname=oracle-${_pkgname}
pkgver=12.2.0.1.0
pkgrel=1
pkgdesc="Light replacement for the Oracle client (files to run OCI, OCCI and JDBC-OCI programs)"
arch=('i686' 'x86_64')
url="http://www.oracle.com/technetwork/database/features/instant-client/"
license=('custom:OTN')
depends=('gcc-libs' 'libaio')
replaces=('instantclient-basic' 'instantclient-basiclite')
options=(!strip)

# These are the only files we want to include in the source package
source=(LICENSE oracle.sh)
md5sums=('2d62e22e4f2d7e6d220fafde0f692a7d'
         '784005aa31cb56bb2303179d98fccd8e')
sha256sums=('f904a30b07ddf7806a33620f93b94c3d315154d26a371ece48695bb3555064a2'
            '36b5ab97950f1667403dd9b59c3cad25d8f9e457702feaece835d1bff7c971c9')

# Add the main file, depending on which architecture we're building for
case "$CARCH" in
	i686)
		source[2]="file:///srv/ftp/${_pkgname}-linux-$pkgver.zip"
		md5sums[2]='afc84c453dd143d6138baa1faf3e3a52'
		sha256sums[2]='32f5a53bdaf65e87b176a68fa0097e2a5249952be6f4afc3e0ef80345591a9cf'
		;;
	x86_64)
		source[2]="file:///srv/ftp/${_pkgname}-linux.x64-$pkgver.zip"
		md5sums[2]='d9639092e3dea2e023272e52e2bd42da'
		sha256sums[2]='5015e3c9fba84e009f7519893f798a1622c37d1ae2c55104ff502c52a0fe5194'
		;;
esac

package() {
	cd "$srcdir/instantclient_12_2/"
	install -d "$pkgdir/usr/bin"
	install -d "$pkgdir/usr/lib"
	install -m 755 -t "$pkgdir/usr/bin" adrci genezi uidrvci
	install -m 755 -t "$pkgdir/usr/lib" *.so*
	install -m 644 -t "$pkgdir/usr/lib" *.jar

	# Copy across the script to set environment variables
	install -Dm755 "$srcdir/oracle.sh" "$pkgdir/etc/profile.d/oracle.sh"

	# create required symlinks
	cd "$pkgdir/usr/lib" || return 1
	local lib link
	for lib in *.so*; do
		link=$lib
		while [[ ${link#*.} != so ]]; do
			link=${link%.*}
			ln -s $lib $link
		done
	done

	install -Dm644 "$srcdir/LICENSE" \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE" || return 1
}
