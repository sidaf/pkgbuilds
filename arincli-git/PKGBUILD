_pkgname='arincli'
pkgname="${_pkgname}-git"
pkgver=143.0cfc966
pkgrel=1
pkgdesc="Command line scripts to query ARIN's Reg-RWS and Whois-RWS services."
url='https://github.com/arineng/arincli'
arch=('any')
license=('CCPL')
depends=('ruby2.3')
makedepends=('git')
options=('!strip' '!emptydirs')
source=('git+https://github.com/arineng/arincli.git')
sha1sums=('SKIP')

pkgver() {
  cd "${_pkgname}"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd "${_pkgname}"

  install -d "$pkgdir/usr/bin" "$pkgdir/usr/share/${pkgname}"
  cp -r * "$pkgdir/usr/share/${pkgname}"

  install -d "$pkgdir/usr/share/man/man1" "$pkgdir/usr/share/man/man7"
  install -Dm644 man/arininfo.1 "$pkgdir/usr/share/man/man1"
  install -Dm644 man/arincli.7 "$pkgdir/usr/share/man/man7"

  local _binaries='arininfo'
  for bin in $_binaries; do
    local _wrapper="$pkgdir/usr/bin/$bin"
    echo -e "#!/bin/sh\nruby-2.3 /usr/share/${pkgname}/bin/$bin \"\${@}\"" > "${_wrapper}"
    chmod 755 "${_wrapper}"
  done
}
