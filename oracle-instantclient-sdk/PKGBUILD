# Contributor: Adam Nielsen <malvineous@shikadi.net>
# Contributor: Vitaliy Berdinskikh <skipper13@archlinux.org.ua>
# Contributor: Andrea Agosti <cifvts@gmail.com>
# Contributor: Viliam Pucik <viliam.pucik@gmail.com>
# Maintainer: Adam Nielsen <malvineous@shikadi.net>

DLAGENTS+=("file::/usr/bin/curl -o %o %u");

_pkgname=instantclient-sdk
pkgname=oracle-${_pkgname}
pkgver=12.1.0.2.0
pkgrel=2
pkgdesc="Additional header files for developing Oracle applications with Instant Client"
arch=('i686' 'x86_64')
url="http://www.oracle.com/technetwork/database/features/instant-client/"
license=('custom:OTN')
depends=(oracle-instantclient-basic=$pkgver)
replaces=('instantclient-sdk')
options=(!strip)

# These are the only files we want to include in the source package
source=(LICENSE)
sha256sums=('f904a30b07ddf7806a33620f93b94c3d315154d26a371ece48695bb3555064a2')

# Add the main file, depending on which architecture we're building for
case "$CARCH" in
	i686)
		source[1]="file:///srv/ftp/${_pkgname}-linux-$pkgver.zip"
		sha256sums[1]='8a3d609230f02186c85cd39bef1e8b974a239254c5b12ba7e482c5a7c35ffb4a'
		;;
	x86_64)
		source[1]="file:///srv/ftp/${_pkgname}-linux.x64-$pkgver.zip"
		sha256sums[1]='ba4b1b96b9f00bc36682542df575f1d52717f6ad48436f50c85b2e87b3a6961c'
		;;
esac

package() {
	cd "$srcdir/instantclient_12_1/"

  # Put all .h files into /usr/include and /usr/lib (ruby-oci8 seems very picky...)

	install -d "$pkgdir/usr/lib"
	install -d "$pkgdir/usr/include"

  cp -r sdk "$pkgdir/usr/lib"
	install -m 644 -t "$pkgdir/usr/include" sdk/include/*.h

	# We don't want this one, it is unused and it conflicts with another
	rm "$pkgdir/usr/include/ldap.h"
}
