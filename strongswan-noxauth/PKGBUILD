# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: dkorzhevin <dkorzhevin at gmail dot com>
# Contributor: Thermi <noel [at] familie-kuntze dot de>
# Contributor: nikicat <develniks at gmail dot com>
# Contributor: danilo <gezuru at gmail dot com>
# Contributor: Jason Begley <jayray at digitalgoat dot com>
# Contributor: Ray Kohler <ataraxia937 at gmail dot com>
# Contributor: Daniel Riedemann <daniel.riedemann [at] googlemail [dot] com>
# Contributor: 458italia <svenskaparadox [at] gmail dot com>
# Contributor: Thermi <noel [at] familie-kuntze dot com>

pkgname=strongswan-noxauth
_pkgname=strongswan
pkgver=5.6.0
pkgrel=3
pkgdesc="open source IPsec implementation"
url='http://www.strongswan.org'
license=("GPL2")
arch=('i686' 'x86_64')
makedepends=('curl' 'gmp' 'iproute2' 'openssl' 'sqlite' 'libcap' 'libsystemd'
'systemd' 'pam' 'libnm-glib' 'python' 'ruby' 'mariadb' 'python-setuptools')
depends=('curl' 'gmp' 'iproute2' 'openssl' 'sqlite' 'libcap' 'libsystemd' 'pam')
optdepends=('libnm-glib: for networkmanager support'
            'mariadb: MySQL support'
            'ruby: Ruby support'
            'python: Python support'
            'openldap: LDAP support')
conflicts=('strongswan')
provides=('strongswan=5.6.0')
backup=(
    etc/ipsec.conf
    etc/ipsec.secrets
    etc/swanctl/swanctl.conf
    etc/strongswan.conf
    etc/strongswan.d/{charon-logging.conf,charon.conf,pki.conf,pool.conf,scepclient.conf,starter.conf,swanctl.conf}
    etc/strongswan.d/charon/{aesni.conf,attr-sql.conf,attr.conf,bliss.conf,chapoly.conf,cmac.conf,connmark.conf,\
constraints.conf,curl.conf,des.conf,dhcp.conf,dnskey.conf,eap-aka-3gpp2.conf,eap-aka.conf,\
eap-gtc.conf,eap-identity.conf,eap-md5.conf,eap-mschapv2.conf,eap-radius.conf,eap-sim-file.conf,\
eap-sim.conf,eap-simaka-pseudonym.conf,eap-simaka-reauth.conf,eap-tls.conf,ext-auth.conf,farp.conf,\
fips-prf.conf,forecast.conf,gmp.conf,ha.conf,hmac.conf,kernel-netlink.conf,md5.conf,mgf1.conf,nonce.conf,newhope.conf,ntru.conf,openssl.conf,\
pem.conf,pgp.conf,pkcs1.conf,pkcs12.conf,pkcs7.conf,pkcs8.conf,pubkey.conf,random.conf,rc2.conf,resolve.conf,\
revocation.conf,sha1.conf,sha2.conf,sha3.conf,socket-default.conf,sql.conf,sqlite.conf,sshkey.conf,stroke.conf,updown.conf,\
vici.conf,x509.conf,xauth-eap.conf,xauth-generic.conf,xcbc.conf,unity.conf,curve25519.conf}
)

source=("https://download.strongswan.org/strongswan-${pkgver}.tar.bz2"
    'configure_ac.patch'
    'stdint.patch'
    'disable-xauth-vendor-id.patch')

sha512sums=('9362069a01c3642e62864d88fdb409a3c7514bf7c92cbe36e552c6a80915119cf5bb91c39592aab2d15b562684a0628a764e4fa7636d3b5fd2ebaf165c0ce649'
            '0e2c818f2f620410dda949d9016a4c1a686bf2946acb3b42a729b2376c077f4dad6762fe8d2f736c213c4895c1fbd60c0d654a1c36f72d06f58ba7cff635bc74'
            'd24ec19bca6faa56c4f29f6d0b785fc70406850d2884b75982b0ca4f285119799f199a9f7dac3e2929a4380fb444d53b939f242c62d0630d5112bf4ac531ca80'
            'ac1be34f4e15d23a92d94e535524c3e5699fd0b8593939ef71f597eb392d2bc5072a7a4c657ed3a5f44d20ccec695e4ae164005b29e442a37ebccec5bcc4c4ec')

# We don't build libipsec because it would get loaded before kernel-netlink and netkey, which
# would case processing to be handled in user space. Also, the plugin is experimental. If you need it,
# add --enable-libipsec and --enable-kernel-libipsec
prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    patch -p1 -l < "${srcdir}/configure_ac.patch"
    patch -p1 -l < "${srcdir}/stdint.patch"
    patch -p1 -l < "${srcdir}/disable-xauth-vendor-id.patch"
    autoreconf
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  ./configure --prefix=/usr \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --with-ipsecdir=/usr/lib/strongswan \
        --with-nm-ca-dir=/etc/ssl/certs \
        --enable-integrity-test \
        --enable-sqlite \
        --enable-openssl --enable-curl \
        --enable-sql --enable-attr-sql \
        --enable-farp --enable-dhcp \
        --enable-eap-sim --enable-eap-sim-file --enable-eap-simaka-pseudonym \
        --enable-eap-simaka-reauth --enable-eap-identity --enable-eap-md5 \
        --enable-eap-gtc --enable-eap-aka --enable-eap-aka-3gpp2 \
        --enable-eap-mschapv2 --enable-eap-radius --enable-xauth-eap \
        --enable-ha --enable-vici --enable-swanctl --enable-systemd --enable-ext-auth \
        --enable-mysql --enable-ldap --enable-cmd --enable-forecast --enable-connmark \
        --enable-aesni --enable-eap-ttls --enable-radattr --enable-xauth-pam --enable-xauth-noauth \
        --enable-eap-dynamic --enable-eap-peap --enable-eap-tls --enable-chapoly --enable-unity \
        --with-capabilities=libcap --enable-newhope --enable-ntru --enable-mgf1 --enable-sha3 \
        --enable-bliss --enable-dnscert \
        --enable-nm --enable-agent --enable-bypass-lan \
        --enable-ruby-gems --enable-python-eggs
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

