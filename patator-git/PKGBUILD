_pkgname='patator'
pkgname="${_pkgname}-git"
pkgver=142.ec605ef
pkgrel=1
pkgdesc='A multi-purpose brute-forcer, with a modular design and a flexible usage.'
url='https://github.com/lanjelot/patator'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('python2' 'openldap' 'ike-scan' 'oracle-instantclient-sdk' 'mariadb' 'postgresql')
makedepends=('git' 'python2-pip')
options=('!strip' '!emptydirs')
source=('git+https://github.com/lanjelot/patator.git')
sha1sums=('SKIP')

pkgver() {
  cd "${_pkgname}"

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_pkgname}"

  install -d "vendor/lib/python2.7/site-packages/"
  cat >> requirements.txt << "EOF"
paramiko==1.7.7.2
pycurl==7.43.0
impacket==0.9.12
ajpy==0.0.1
cx_Oracle==5.1.3
MySQL-python==1.2.5
psycopg2==2.7.3
pycrypto==2.6.1
dnspython==1.15.0
IPy==0.83
pysnmp==4.3.8
pyasn1==0.3.1
EOF
  PYTHONUSERBASE="${srcdir}/${_pkgname}/vendor" pip2 install --user --ignore-installed --requirement requirements.txt
}

package() {
  cd "${_pkgname}"

  install -d "$pkgdir/usr/bin" "$pkgdir/usr/share/${pkgname}"
  cp -r * "$pkgdir/usr/share/${pkgname}"

  local _binaries="${_pkgname}.py"
  for bin in $_binaries; do
    local _wrapper="$pkgdir/usr/bin/$bin"
    echo -e "#!/bin/sh\nPYTHONPATH="/usr/share/${pkgname}:/usr/share/${pkgname}/vendor/lib/python2.7/site-packages" exec python2 /usr/share/${pkgname}/$bin \"\${@}\"" > "${_wrapper}"
    chmod 755 "${_wrapper}"
  done
}
