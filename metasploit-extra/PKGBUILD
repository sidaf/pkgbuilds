_pkgname=metasploit
pkgname=${_pkgname}-extra
pkgver=4.16.13
pkgrel=1
pkgdesc='Advanced open-source platform for developing, testing, and using exploit code. With Oracle support included.'
url='https://www.metasploit.com/'
arch=('i686' 'x86_64')
license=('BSD')
depends=('ruby' 'libpcap' 'postgresql-libs' 'ruby-bundler' 'sqlite' 'libxslt' 'git' 'oracle-instantclient-sdk' 'oracle-instantclient-basic')
optdepends=('ruby-pg: database support')
options=('!strip' '!emptydirs')
conflicts=('metasploit')
source=(${_pkgname}-${pkgver}.tar.gz::https://github.com/rapid7/metasploit-framework/archive/${pkgver}.tar.gz)
sha256sums=('SKIP')

prepare() {
  cd ${_pkgname}-framework-${pkgver}
  bundle config build.nokogiri --use-system-libraries
  sed 's|git ls-files|find -type f|' -i metasploit-framework.gemspec
}

build() {
  cd ${_pkgname}-framework-${pkgver}
  bundle install -j"$(nproc)" --no-cache --deployment

  cat >> Gemfile.local << "EOF"
group :local do
  gem 'ruby-oci8', '~> 2.2.3'
end
EOF

  cat >> Gemfile.local.lock << "EOF"
GEM
  remote: https://rubygems.org/
  specs:
    ruby-oci8 (2.2.3)

PLATFORMS
  ruby

DEPENDENCIES
  ruby-oci8 (~> 2.2.3)

BUNDLED WITH
  1.15.4
EOF
  bundle install --gemfile Gemfile.local --no-cache --deployment

  find vendor/bundle/ruby -exec chmod o+r '{}' \;
}

package() {
  cd ${_pkgname}-framework-${pkgver}

  install -d "${pkgdir}/opt/${_pkgname}" "${pkgdir}/usr/bin"
  cp -r . "${pkgdir}/opt/${_pkgname}"

  for f in "${pkgdir}"/opt/${_pkgname}/msf*; do
    local _msffile="${pkgdir}/usr/bin/`basename "${f}"`"
    echo -e "#!/bin/sh\nBUNDLE_GEMFILE=/opt/${_pkgname}/Gemfile bundle exec ruby /opt/${_pkgname}/`basename "${f}"` \"\$@\"" > "${_msffile}"
    chmod 755 "${_msffile}"
  done

  (cd "${pkgdir}/opt/${_pkgname}"
    for f in tools/*/*.rb; do
      install -Dm 755 "${f}" ".${f}"
      echo -e "#!/bin/sh\nBUNDLE_GEMFILE=/opt/${_pkgname}/Gemfile bundle exec ruby /opt/${_pkgname}/."${f}" \"\$@\"" > "${f}"
      chmod 755 "${f}"
    done
  )

  install -Dm 644 external/zsh/_* -t "${pkgdir}/usr/share/zsh/site-functions"
  install -Dm 644 LICENSE COPYING -t "${pkgdir}/usr/share/licenses/${_pkgname}"
  install -d "${pkgdir}/usr/share/doc"
  mv "${pkgdir}/opt/${_pkgname}/documentation" "${pkgdir}/usr/share/doc/${_pkgname}"
  rm "${pkgdir}/usr/bin/msfupdate"
}

# vim: ts=2 sw=2 et:
